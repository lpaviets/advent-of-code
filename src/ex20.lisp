(in-package #:aoc2021/ex20)

(declaim (optimize (debug 3) (safety 0) (speed 0)))

(defparameter *rules* nil)

(defun parse-rules (line)
  (setf *rules* line))

(defun unrestricted-neighbours (x y)
  (loop :for i :from (1- x) :upto (1+ x)
        :append (loop :for j :from (1- y) :upto (1+ y)
                      :collect (list i j))))

(defun get-unrestricted-neighbours (x y array)
  (loop :for (i j) :in (unrestricted-neighbours x y)
        :collect (if (and (array-in-bounds-p array i j)
                          (char= (aref array i j) #\#))
                     1
                     0)))

(defun convoluted-value (x y array)
  (let* ((binary-str (format nil "狺ㄧ弭躅蝈篝蜷泗邃铄殓桠秕蝮狎蜥┅ㄩ钿屮疳蝮瀛轭翦珏忾钺蝙篝候徜轼博┅ㄣ栳蝓戾螵轭溴┅ㄤ彐躅篦狃疳滗轭ㄡ蝌狴ㄦ戾è蝈ㄣㄩㄣ栳蚪＼）＼＼）┅戾舄èㄡ蝌狴溟礤铙轱狎蜥癌ㄡ蝌狴溟礤铙轱狎蜥暴┅祜镳烘矧衡屐秣轰箦翩ㄡ蝈狎蜥戛蝈ㄡ蝈狎蜥戛ㄡ蝈狎蜥ū瑭戛蝈ㄡ蝈狎蜥ū瑭戛┅祜镳烘矧烘蝻衡屐秣ū瑭轰箦翩ㄡ蝈狎蜥癌蝈ㄡ蝈狎蜥癌ㄡ蝈狎蜥ū鳗蝈ㄡ蝈狎蜥ū鳗┅┅┅ㄤ彐躅疳洵狎蜥ㄡ蝌狴鲠祯濠戾è铄磲脲狎蜥扉篝ǐㄡ蝌狴溟礤铙轱狎蜥癌ǐㄡ蝌狴溟礤铙轱狎蜥暴┅洪铋糸犰屐屙孱鲠祯濠┅ㄤ锃狎蜥ㄩ狎蜥铄鳗箦翩ㄡ蝈铄ū椹ū戛┅铄鳗ㄤ彐躅磲脲泔铞镬豸轱瞽篝屦ㄡ蝌狴镳糸镱犰篝屦暴⒘篌蹴翳狒蝓戾ò爆蝓戾ǖ辈阿祜镳瑚轸铄烘矧蝈篚祠疳洵狎蜥狎蜥＼呼桢痱镧箦翩蝈篚祠疳洵狎蜥蝈篚祠疳洎戾舄èㄡ蝌狴溟礤铙轱蝈篚祠癌ㄡ蝌狴溟礤铙轱蝈篚祠暴铄磲脲狎蜥扉篝鳗洪铋糸犰屐屙孱疳洎┅ㄤ锃狎蜥ㄩ蝈篚祠麒孱ㄡ钿ū瑭ū鳗┅箦翩ㄡ蝈铄戛ㄣ镱鲲祯翦洵鲠祯蝈篚祠┅┅篦狃疳滗轭铄鳗铄鳗烘矧篝屦烘蝻衡屐秣篝屦烘矧疳ㄩ弪镳盹篝屦博＼＼）烘轭犰禊蝈趱蝾蝈篚祠┅ㄤ彐躅泔躅舡扉舡痖屐ㄡ蝌狴戾è泔躅癌ㄨㄡ蝌狴溟礤铙轱狎蜥癌ㄡ蝌狴溟礤铙轱狎蜥暴┅ㄤ锃狎蜥ㄩ狎蜥麒孱ㄡ钿冀ū瑭冀ū鳗ㄣ栳蚪＼）ㄩ钽泔躅舂┅泔躅舂ㄤ彐躅翦篝屮舶ī戾舄è扉铄Ж．．＃＃．．．．＃．＃＃．＃．＃．＃＃＃＃．．．．＃＃．＃＃＃＃．＃＃．＃＃．＃．＃＃．＃．．．＃．．．＃．＃＃＃＃．＃＃．＃＃．．＃＃．．．＃．．＃．．．．＃．＃＃＃＃＃＃＃＃．．．．．．．＃＃＃．．．＃．＃．＃．＃＃．．．．．．．＃＃＃．＃．＃＃．．．＃．＃．＃＃＃＃．．．．．＃＃＃．．．．＃．．＃．＃＃．．＃＃．＂．．．＃＂．＃＂换蝈徜骈戾狍扉铄轭瘐趔轭瘐舨爱豇簪ㄡ蝌狴蝈徜狎蜥ㄣ滗扉铄螬铋飑┅疳蝮瀛蝓戾ㄣ狎扉铄螬痱轭舡狎蜥磲脲泔铞镬豸轱瞽篝屦┅┅ㄤ彐躅犷篦弪屮舶ī戾舄è扉铄蝈徜骈戾狍扉铄轭瘐趔轭瘐舨爱豇簪┅ㄡ蝌狴蝈徜狎蜥ㄣ滗扉铄螬铋飑┅疳蝮瀛蝓戾ㄣ狎扉铄螬ㄣ秕铘扉舡痖屐磲脲泔铞镬豸轱瞽篝屦狎蜥博┅ㄤ彐躅犷篦弪屮舶īㄤ邈灬蝈镳糸黹箴邋畅ㄤ邂蹒癌筢驽豉癌┅戾舄è扉铄蝈徜骈戾狍扉铄轭瘐趔轭瘐舨爱豇簪┅ㄡ蝌狴蝈徜狎蜥ㄣ滗扉铄螬铋飑┅疳蝮瀛蝓戾ㄣ狎扉铄螬ㄣ秕铘扉舡痖屐磲脲泔铞镬豸轱瞽篝屦狎蜥蛋┅┅